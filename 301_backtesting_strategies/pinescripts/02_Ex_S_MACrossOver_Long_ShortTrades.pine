//Â© Sergey Buzz - Mastering TradingView Pine Script v5

//@version=5
strategy("Moving average crossing strategy", overlay=true, default_qty_type = strategy.percent_of_equity, default_qty_value = 100, initial_capital = 1000, commission_type = strategy.commission.percent, commission_value = 0.1)

StartDay = input.int(1, "Start Day")
StartMonth = input.int(1, "Start Month")
StartYear = input.int(2010, "Start Year")

EndDay = input.int(1, "End Day")
EndMonth = input.int(1, "End Month")
EndYear = input.int(2025, "End Year")

TimeStart = timestamp(StartYear, StartMonth, StartDay, 0, 0)
TimeEnd = timestamp(EndYear, EndMonth, EndDay, 0, 0)

IsTimeEnabled() =>
    time >= TimeStart and time <= TimeEnd ? true : false    

//Setup strategy parameters 
fastMA = input.int(12, "Fast MA Period", 1, 200, 5)
slowMA = input.int(26, "Slow MA Period", 1, 200, 5)

TradeType = input.string(title="Trade Type", defval="LongAndShort", options = ["LongAndShort", "Long", "Short"])

//Calculate fast and slow moving averages
fastMAValue = ta.ema(close, fastMA)
slowMAValue = ta.ema(close, slowMA)

if(IsTimeEnabled())
    if (fastMAValue > slowMAValue)
        if(TradeType != "Short")
            strategy.entry("Long", strategy.long)
        else if(strategy.position_size < 0)
            strategy.close("Short")
    else if (fastMAValue < slowMAValue)
        if(TradeType != "Long")
            strategy.entry("Short", strategy.short)
        else if(strategy.position_size > 0)
            strategy.close("Long")

//Draw moving averages
plot(fastMAValue, "Fast MA", color.blue, 2)
plot(slowMAValue, "Slow MA", color.maroon, 2)